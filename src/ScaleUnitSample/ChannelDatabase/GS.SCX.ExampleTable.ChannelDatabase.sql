/**
 * SAMPLE CODE NOTICE
 * 
 * THIS SAMPLE CODE IS MADE AVAILABLE AS IS.  MICROSOFT MAKES NO WARRANTIES, WHETHER EXPRESS OR IMPLIED,
 * OF FITNESS FOR A PARTICULAR PURPOSE, OF ACCURACY OR COMPLETENESS OF RESPONSES, OF RESULTS, OR CONDITIONS OF MERCHANTABILITY.
 * THE ENTIRE RISK OF THE USE OR THE RESULTS FROM THE USE OF THIS SAMPLE CODE REMAINS WITH THE USER.
 * NO TECHNICAL SUPPORT IS PROVIDED.  YOU MAY NOT DISTRIBUTE THIS CODE UNLESS YOU HAVE A LICENSE AGREEMENT WITH MICROSOFT THAT ALLOWS YOU TO DO SO.
 */

 -- Create the extension table to store the custom fields.

IF (SELECT OBJECT_ID('[ext].[GS.SCX_EXAMPLETABLE]')) IS NULL 
BEGIN
    CREATE TABLE
        [ext].[GS.SCX_EXAMPLETABLE]
    (
        [EXAMPLEID]     BIGINT IDENTITY(1,1) NOT NULL,
        [EXAMPLEINT]    INT NOT NULL DEFAULT ((0)),
        [EXAMPLESTRING] NVARCHAR(64) NOT NULL DEFAULT (('')),
        CONSTRAINT [I_EXAMPLETABLE_EXAMPLEID] PRIMARY KEY CLUSTERED 
        (
            [EXAMPLEID] ASC
        ) WITH (PAD_INDEX = OFF, STATISTICS_NORECOMPUTE = OFF, IGNORE_DUP_KEY = OFF, ALLOW_ROW_LOCKS = ON, ALLOW_PAGE_LOCKS = ON) ON [PRIMARY]
    ) ON [PRIMARY]

    ALTER TABLE [ext].[GS.SCX_EXAMPLETABLE] WITH CHECK ADD CHECK (([EXAMPLEID]<>(0)))
END
GO

GRANT SELECT, INSERT, UPDATE, DELETE ON OBJECT::[ext].[GS.SCX_EXAMPLETABLE] TO [DataSyncUsersRole]
GO

-- Create a stored procedure CRT can use to add entries to the custom table.

IF OBJECT_ID(N'[ext].[GS.SCX_INSERTEXAMPLE]', N'P') IS NOT NULL
    DROP PROCEDURE [ext].[GS.SCX_INSERTEXAMPLE]
GO

CREATE PROCEDURE [ext].[GS.SCX_INSERTEXAMPLE]
    @i_ExampleInt    INT,
    @s_ExampleString NVARCHAR(64)
AS
BEGIN
    SET NOCOUNT ON

    INSERT INTO
         [ext].[GS.SCX_EXAMPLETABLE]
        (EXAMPLEINT, EXAMPLESTRING)
    OUTPUT
        INSERTED.EXAMPLEID
    VALUES
        (@i_ExampleInt, @s_ExampleString)
END;
GO

GRANT EXECUTE ON [ext].[GS.SCX_INSERTEXAMPLE] TO [UsersRole];
GO

GRANT EXECUTE ON [ext].[GS.SCX_INSERTEXAMPLE] TO [DeployExtensibilityRole];
GO

-- Create the custom view that can query a complete Example Entity.

IF (SELECT OBJECT_ID('[ext].[GS.SCX_EXAMPLEVIEW]')) IS NOT NULL
    DROP VIEW [ext].[GS.SCX_EXAMPLEVIEW]
GO

CREATE VIEW [ext].[GS.SCX_EXAMPLEVIEW] AS
(
    SELECT
        et.EXAMPLEINT,
        et.EXAMPLESTRING,
        et.EXAMPLEID
    FROM
        [ext].[GS.SCX_EXAMPLETABLE] et
)
GO

GRANT SELECT ON OBJECT::[ext].[GS.SCX_EXAMPLEVIEW] TO [UsersRole];
GO

GRANT SELECT ON OBJECT::[ext].[GS.SCX_EXAMPLEVIEW] TO [DeployExtensibilityRole];
GO

-- Create a stored procedure CRT can use to perform updates.

IF OBJECT_ID(N'[ext].[GS.SCX_UPDATEEXAMPLE]', N'P') IS NOT NULL
    DROP PROCEDURE [ext].[GS.SCX_UPDATEEXAMPLE]
GO

CREATE PROCEDURE [ext].[GS.SCX_UPDATEEXAMPLE]
    @bi_Id           BIGINT,
    @i_ExampleInt    INT,
    @s_ExampleString NVARCHAR(64)
AS
BEGIN
    SET NOCOUNT ON

    UPDATE
        [ext].[GS.SCX_EXAMPLETABLE]
    SET
        EXAMPLEINT = @i_ExampleInt,
        EXAMPLESTRING = @s_ExampleString
    WHERE
        EXAMPLEID = @bi_Id
END;
GO

GRANT EXECUTE ON [ext].[GS.SCX_UPDATEEXAMPLE] TO [UsersRole];
GO

GRANT EXECUTE ON [ext].[GS.SCX_UPDATEEXAMPLE] TO [DeployExtensibilityRole];
GO

-- Create a stored procedure CRT can use to delete Example Entities.

IF OBJECT_ID(N'[ext].[GS.SCX_DELETEEXAMPLE]', N'P') IS NOT NULL
    DROP PROCEDURE [ext].[GS.SCX_DELETEEXAMPLE]
GO

CREATE PROCEDURE [ext].[GS.SCX_DELETEEXAMPLE]
    @bi_Id           BIGINT
AS
BEGIN
    SET NOCOUNT ON

    DELETE FROM
        [ext].[GS.SCX_EXAMPLETABLE]
    WHERE
        EXAMPLEID = @bi_Id
END;
GO

GRANT EXECUTE ON [ext].[GS.SCX_DELETEEXAMPLE] TO [UsersRole];
GO

GRANT EXECUTE ON [ext].[GS.SCX_DELETEEXAMPLE] TO [DeployExtensibilityRole];
GO
